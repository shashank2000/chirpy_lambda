%import .utils (STRING, PARAM_ARG_NAME, PARAM_ARG_VALUE, SUBNODE_NAME, _PERIOD, _OP, _CP, _OB, _CB, _COMMA, _EQ, variable)
%import common.ESCAPED_STRING

PUNCTUATION: /[?.,;]/

nlg : "{" ("-"? nlg)* "}"
	| PUNCTUATION
	| val
	| neural_generation
	| helper
	| inflect
	| inflect_engine
	| one_of
	| constant
	| lookup
	| ESCAPED_STRING
	| "\"" "\""
	
LIST : _OB _CB
	 | _OB

OPERATORS: "|" | "+" | "-"

CONSTANT: STRING | "{}"

key: "[" nlg "]"

val : "@val" _OP variable key* (OPERATORS PARAM_ARG_NAME)* _CP

neural_generation : "@neural_generation" _OP "prefix" _EQ nlg _CP

helper : "@helper" _PERIOD PARAM_ARG_NAME _OP (nlg (_COMMA nlg)*)? _CP

inflect : "@inflect" _OP "inflect_token" _EQ nlg _COMMA "inflect_entity" _EQ variable _CP

inflect_engine : "@inflect_engine" _OP "type" _EQ nlg _COMMA "str" _EQ nlg _CP

lookup : "@lookup" _OP "database_name" _EQ ESCAPED_STRING _COMMA "key" _EQ nlg _COMMA "column" _EQ ESCAPED_STRING _CP

one_of : "@one_of" _OP (nlg (_COMMA nlg)* _COMMA?)? _CP

constant : "@constant" _OP CONSTANT _CP