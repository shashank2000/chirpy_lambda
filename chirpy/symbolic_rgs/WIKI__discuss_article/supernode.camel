<ENTRY_CONDITIONS>
not IS_NONE(State.WIKI__Entity) and IS_TRUE(Flags.WIKI__discuss_article)

<ENTRY_LOCALS>
Locals.suggested_sections = @val(State.WIKI__suggested_sections[@val(State.WIKI__Entity), @constant([])])
Locals.discussed_sections = @val(State.WIKI__discussed_sections[@val(State.WIKI__Entity), @constant([])])
Locals.last_discussed_section = @val(State.WIKI__last_discussed_section[@val(State.WIKI__Entity), @constant(None)])

Locals.get_sections_result = @helper.get_sections(@val(State.WIKI__Entity), @val(Locals.suggested_sections), @val(Locals.discussed_sections), @val(Locals.last_discussed_section))
Locals.parent_section = @val(Locals.get_sections_result["parent_section"])
Locals.sections = @val(Locals.get_sections_result["sections"])
Locals.repeat = @val(Locals.get_sections_result["repeat"])

State.WIKI__currently_chosen_sections = @helper.choose_from_sections(@val(Locals.sections))
Locals.get_section_choices_result = @helper.get_section_choices(@val(State.WIKI__Entity), @val(State.WIKI__currently_chosen_sections), @val(Locals.parent_section))
Locals.section_choices = @val(Locals.get_section_choices_result["section_choices"])
Locals.entitys_section_choices = @val(Locals.get_section_choices_result["entitys_section_choices"])

<PROMPTS>
Prompt discuss_article_section [
	IS_NONE(Locals.parent_section) and IS_FALSE(Locals.repeat)
] {
	- @one_of(
		{
			- "You know, I happen to know a thing or two about"
			- @val(Locals.entitys_section_choices)
			- ", if you're interested in talking about"
			- @helper.any_of(@val(State.WIKI__currently_chosen_sections))
			- "that?"
		}, {
			- "I find"
			- @val(Locals.entitys_section_choices)
			- "rather fascinating. Would you care to hear about"
			- @helper.any_of(@val(State.WIKI__currently_chosen_sections))
			- "that?"
		}, {
			- "Come to think of it, I did read something the other day about"
			- @val(Locals.entitys_section_choices)
			- ", if you're interested in hearing about"
			- @helper.any_of(@val(State.WIKI__currently_chosen_sections))
			- "that."
		}, {
			- "Personally, I think the"
			- @val(Locals.section_choices)
			- @helper.of_entity(@val(State.WIKI__Entity), @val(Locals.section_choices))
			- @helper.is_are(@val(State.WIKI__currently_chosen_sections))
			- "quite remarkable. Are you interested in talking about"
			- @helper.any_of(@val(State.WIKI__currently_chosen_sections))
			- "that?"
		}, {
			- "The"
			- @val(Locals.section_choices)
			- @helper.of_entity(@val(State.WIKI__Entity), @val(Locals.section_choices))
			- @helper.is_are(@val(State.WIKI__currently_chosen_sections))
			- "really interesting. Would you like to talk about"
			- @helper.any_of(@val(State.WIKI__currently_chosen_sections))
			- "that?"
		}
	)
}

Prompt discuss_article_section_repeat [
	IS_NONE(Locals.parent_section) and IS_TRUE(Locals.repeat)
] {
	- @one_of(
		{
			- "If you'd like, we could also talk about"
			- @val(Locals.entitys_section_choices)
			- "."
		}, {
    	- "Well, I've also read about"
			- @val(Locals.entitys_section_choices)
			- ","
			- "if that sounds interesting to you."
		}, {
      - "I think"
			- @val(Locals.entitys_section_choices)
			- @helper.is_are(@val(State.WIKI__currently_chosen_sections))
			- "pretty interesting too."
			- "Wanna hear about"
			- @helper.them_it(@val(State.WIKI__currently_chosen_sections))
			- "?"
		}, {
			- @val(Locals.entitys_section_choices)
      - "are rather fascinating. Do you want to hear more about"
			- @helper.them_it(@val(State.WIKI__currently_chosen_sections))
			- "?"
		}
	)
}

Prompt discuss_article_subsection [
	not IS_NONE(Locals.parent_section)
] {
	- @one_of(
		{
			- "Well, since we are on the topic of"
			- @helper.section_string(@val(State.WIKI__Entity), @val(Locals.parent_section))
			- "do you have any interest in talking about"
			- @val(Locals.entitys_section_choices)
			- "?"
		}, {
			- "Well, since we are talking about"
			- @helper.section_string(@val(State.WIKI__Entity), @val(Locals.parent_section))
			- "are you interested in hearing about"
			- @val(Locals.entitys_section_choices)
			- "?"
		}, {
			- "Speaking of"
			- @helper.section_string(@val(State.WIKI__Entity), @val(Locals.parent_section))
			- "are you interested in hearing about"
			- @val(Locals.entitys_section_choices)
			- "?"
		}
	)
}

<LOCALS>
Locals.suggested_sections = @val(State.WIKI__suggested_sections[@val(State.WIKI__Entity), @constant([])])
Locals.discussed_sections = @val(State.WIKI__discussed_sections[@val(State.WIKI__Entity), @constant([])])

Locals.selected_section = @helper.get_selected_section(@val(State.WIKI__Entity), @val(Locals.suggested_sections), @val(State.WIKI__currently_chosen_sections), @val(Utilities.response_text), @val(Flags.GlobalFlag__YES))
Locals.section_summary = @helper.get_section_summary(@val(Locals.selected_section), @val(Utilities.state_manager))

<SET_STATE>

<SUBNODES>
Subnode discuss_section [
	not IS_NONE(Locals.selected_section) and not IS_NONE(Locals.section_summary)
] {
	- @one_of(
		"So, about",
		"Great, so regarding"
	)
	- @helper.get_entitys_section(@val(State.WIKI__Entity), @val(Locals.selected_section))
	- ","
	- @val(Locals.section_summary)
}

Subnode default [
] {
	- @neural_generation(prefix="")
} <
	State.WIKI__Entity = @constant(None)
	Flags.WIKI__discuss_article = @constant(False)
>

<SET_STATE_AFTER>
State.WIKI__suggested_sections[@val(State.WIKI__Entity)] = @helper.concat(@val(Locals.suggested_sections), @val(State.WIKI__currently_chosen_sections))
State.WIKI__discussed_sections[@val(State.WIKI__Entity)] = @helper.append(@val(Locals.discussed_sections), @val(Locals.selected_section))
State.WIKI__last_discussed_section[@val(State.WIKI__Entity)] = @val(Locals.selected_section)
State.WIKI__currently_chosen_sections = @constant([])